if (isLibrary) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion versions.build.compile_sdk

    defaultConfig {
        if (!isLibrary) {
            applicationId "com.icheero.reverse"
        }
        minSdkVersion versions.build.min_sdk
        targetSdkVersion versions.build.target_sdk
        versionCode versions.build.code
        versionName versions.build.name
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
                includeCompileClasspath true
            }
        }
    }

    signingConfigs {
        release {
            keyAlias versions.sign.release.alias
            keyPassword versions.sign.release.password
            storeFile file(versions.sign.release.filepath)
            storePassword versions.sign.release.store
        }
        debug {
            keyAlias versions.sign.debug.alias
            keyPassword versions.sign.debug.password
            storeFile file(versions.sign.debug.filepath)
            storePassword versions.sign.debug.store
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            if (isLibrary) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility versions.build.java
        targetCompatibility versions.build.java
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':CheeroSDK')
    annotationProcessor versions.deps.arouter_compiler
}
